
// === silents =================
WHITESPACE = _{ " " | "\t" | "\\" | NEWLINE }
COMMENT = _{ "//" ~ ( !NEWLINE ~ ANY )* ~ NEWLINE }

// === primitive =================
POS_NEG      = { "+" | "-" }
ASCII_DIGITS = { ASCII_DIGIT+ }

// === reserved keyword =================
KW_SELECT = @{ ^"SELECT" ~ !ASCII_ALPHANUMERIC }
KW_FROM   = @{ ^"FROM"  ~ !ASCII_ALPHANUMERIC }
KW_WITH   = @{ ^"WITH" ~ !ASCII_ALPHANUMERIC  }
KW_WHERE  = @{ ^"WHERE"  ~ !ASCII_ALPHANUMERIC }
KW_ASTERISK = @{ "*" ~ !ASCII_ALPHANUMERIC }

KW_TIMESTAMP  = @{ ^"ts" ~ !ASCII_ALPHANUMERIC }
KW_METRICS    = @{ ^"metrics" ~ !ASCII_ALPHANUMERIC }

KW_AND  = @{ ^"AND" ~ !ASCII_ALPHANUMERIC }
KW_OR  = @{ ^"OR" ~ !ASCII_ALPHANUMERIC }

KW_ASC  = @{ ^"ASC" ~ !ASCII_ALPHANUMERIC }
KW_DESC = @{ ^"DESC" ~ !ASCII_ALPHANUMERIC }

KW_COLS    = @{ ^"COLS" ~ !ASCII_ALPHANUMERIC }
KW_TZ      = @{ ^"TZ" ~ !ASCII_ALPHANUMERIC }
KW_FORMAT  = @{ ^"FORMAT" ~ !ASCII_ALPHANUMERIC }
KW_OUTPUT_FILE = @{ ^"OUTPUT_FILE" ~ !ASCII_ALPHANUMERIC }
KW_OUTPUT_MEMORY = @{ ^"OUTPUT_TO_MEMORY" }
KW_ORDER   = @{ ^"ORDER" ~ !ASCII_ALPHANUMERIC }
KW_BY      = @{ ^"BY" ~ !ASCII_ALPHANUMERIC }
KW_USE_CACHE   = @{ ^"USE_CACHE" ~ !ASCII_ALPHANUMERIC }
KW_FORMAT_DATETIME   = @{ ^"FORMAT_DATETIME" ~ !ASCII_ALPHANUMERIC }
KW_FORCE_SYNC_CLOUD  = @{ ^"FORCE_SYNC_CLOUD" ~ !ASCII_ALPHANUMERIC }
KW_DB          = @{ ^"db" ~ !ASCII_ALPHANUMERIC }

KW_TRUE      = @{ ^"TRUE" ~ !ASCII_ALPHANUMERIC }
KW_FALSE      = @{ ^"FALSE" ~ !ASCII_ALPHANUMERIC }

KW_MICROSECOND = @{ ^"MICRO" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }
KW_MILLISECOND = @{ ^"MILLI" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }
KW_SECOND  		= @{ ^"SECOND" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }
KW_MINUTES  	= @{ ^"MINUTE" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }
KW_HOUR  			= @{ ^"HOUR" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }
KW_DAY  			= @{ ^"DAY" ~ ^"S"? ~ !ASCII_ALPHANUMERIC }

KW_JSON  			= @{ ^"JSON"  ~ !ASCII_ALPHANUMERIC }
KW_TABLE      = @{ ^"TABLE" ~ !ASCII_ALPHANUMERIC }
KW_PARQUET    = @{ ^"PARQUET" ~ !ASCII_ALPHANUMERIC }
KW_PARQUET_SNAPPY    = @{ ^"PARQUET_SNAPPY" ~ !ASCII_ALPHANUMERIC }

KW_SEMICOLON  = @{ ^";" ~ !ASCII_ALPHANUMERIC }

BOOLEAN_VALUE = { KW_TRUE | KW_FALSE }

DURATION_UNIT = { KW_MICROSECOND | KW_MILLISECOND | KW_SECOND | KW_MINUTES | KW_HOUR	| KW_DAY }
FILE_PATH = @{ (ASCII_ALPHANUMERIC| "." | "/" | "\\" |"-"|"_")+}

// === selector =================
STAR_SELECTOR =  { "*" }

CLOCK = { (ASCII_DIGIT | ":")+ }

TIMEZONE_OFFSET_VAL = @{ POS_NEG ~ CLOCK }
TIMEZONE_NAME = @{ ASCII_ALPHA{3,5} ~ !ASCII_ALPHANUMERIC }

COLUMN_NAME_AVAILABLE_SYMBOL = { "_" | "-"}
COLUMN_NAME = @{ ( ASCII_ALPHANUMERIC | COLUMN_NAME_AVAILABLE_SYMBOL )+ }

DB_NAME_AVAILABLE_SYMBOL = { "_" | "-"}
DB_NAME =     @{ ( ASCII_ALPHANUMERIC | DB_NAME_AVAILABLE_SYMBOL )+ }

//TODO(tacogips) exclude KWs from column names
COLUMNS = { COLUMN_NAME  ~ ( "," ~  COLUMN_NAME )* ~ !"," }

METRICS_NAME_AVAILABLE_SYMBOL = { "_" | "-" | "."}
METRICS_NAME = @{ (ASCII_ALPHANUMERIC | METRICS_NAME_AVAILABLE_SYMBOL )+ }

DEFINE_DATABASE = { KW_DB ~  "=" ~ DB_NAME }
DEFINE_COLUMNS = { KW_COLS ~ "=" ~  "[" ~ COLUMNS ~ "]" }
DEFINE_TZ      = { KW_TZ ~ "=" ~ ( TIMEZONE_OFFSET_VAL | TIMEZONE_NAME ) }
DEFINE_FORMAT  = { KW_FORMAT ~ "=" ~ ( KW_JSON | KW_TABLE | KW_PARQUET_SNAPPY | KW_PARQUET ) }
DEFINE_OUTPUT_FILE  = { KW_OUTPUT_FILE ~ "=" ~ "'" ~ FILE_PATH ~ "'"}
DEFINE_OUTPUT_MEMORY = { KW_OUTPUT_MEMORY}
DEFINE_FORMAT_DATETIME   = { KW_FORMAT_DATETIME ~ "=" ~ BOOLEAN_VALUE }
DEFINE_CACHE   = { KW_USE_CACHE ~ "=" ~ BOOLEAN_VALUE }
DEFINE_CLOUD   = { KW_FORCE_SYNC_CLOUD  ~ "=" ~ BOOLEAN_VALUE }

DATETIME_STR = { "'" ~ ( ASCII_ALPHANUMERIC | "." | " " | "-" | ":") + ~ "'"}
FN_NOW     = { ^"NOW()" }
FN_TODAY     = { ^"TODAY()" }
FN_YESTERDAY = { ^"YESTERDAY()" }
FN_TOMORROW  = { ^"TOMORROW()" }

CLOCK_DELTA        = { POS_NEG? ~ CLOCK }
DURATION_DELTA     = { POS_NEG? ~ ASCII_DIGITS ~ DURATION_UNIT }
DATETIME_DELTA     = { DURATION_DELTA | CLOCK_DELTA }
DATETIME_FN        = { FN_NOW | FN_TODAY | FN_YESTERDAY | FN_TOMORROW }

SIGNED_DIGITS      = {POS_NEG? ~ ASCII_DIGITS}

DATETIME =   { ( DATETIME_FN  | DATETIME_STR ) ~ DATETIME_DELTA? }
DATETIME_RANGE_CLOSE = { DATETIME_DELTA | DATETIME }
DATETIME_RANGE =  { "(" ~ DATETIME ~"," ~ DATETIME_RANGE_CLOSE ~ ")"}

REL_IN  = { ^"IN" }
REL_GTE = { ">=" }
REL_GTE_2 = { "=>" }
REL_GT  = { ">" }
REL_LTE = { "<=" }
REL_LTE_2 = { "=<" }
REL_LT  = { "<" }
REL_EQ  = { "=" }
REL_FILTER_SYMBOL = @{ "|" }

REL_GTE_LIMIT = @{ ( REL_GTE | REL_GTE_2 ) ~ REL_FILTER_SYMBOL ~ ASCII_DIGITS}
REL_GT_LIMIT  = @{ REL_GT ~ REL_FILTER_SYMBOL ~ ASCII_DIGITS}
REL_LTE_LIMIT = @{ ( REL_LTE | REL_LTE_2 ) ~ REL_FILTER_SYMBOL ~ ASCII_DIGITS}
REL_LT_LIMIT  = @{ REL_LT ~ REL_FILTER_SYMBOL ~ ASCII_DIGITS}

REL_OP = {
	REL_GTE_LIMIT  |
	REL_GT_LIMIT   |
	REL_LTE_LIMIT  |
	REL_LT_LIMIT   |
	REL_IN |
	REL_GTE |
	REL_GTE_2 |
	REL_GT |
	REL_LTE |
	REL_LTE_2 |
	REL_LT |
	REL_EQ
}

FILTER = { DATETIME_FILTER | METRICS_FILTER }
DATETIME_FILTER = { KW_TIMESTAMP ~ REL_OP ~ ( DATETIME_RANGE | DATETIME )}
METRICS_FILTER  = { KW_METRICS   ~ "=" ~ METRICS_NAME }

// === WITH CLAUSE =========================================
WITH_CLAUSE   = { KW_WITH ~ WITH_CLAUSE_DEFINES  ~ ("," ~ WITH_CLAUSE_DEFINES )* }
WITH_CLAUSE_DEFINES = { DEFINE_TZ  | DEFINE_COLUMNS | DEFINE_FORMAT  | DEFINE_OUTPUT_FILE | DEFINE_OUTPUT_MEMORY | DEFINE_CACHE | DEFINE_CLOUD | DEFINE_DATABASE |DEFINE_FORMAT_DATETIME    }

SELECT_CLAUSE = { KW_SELECT ~ ( KW_ASTERISK | COLUMNS ) }
FROM_CLAUSE   = { KW_FROM ~ METRICS_NAME  }

WHERE_CLAUSE  = { KW_WHERE ~ FILTER } // only simple condition is allowed now

// === Query =================
QUERY = {
		SOI
	~ WITH_CLAUSE?
	~ SELECT_CLAUSE
	~ FROM_CLAUSE
	~ WHERE_CLAUSE?
	~ KW_SEMICOLON?
	~ EOI }

